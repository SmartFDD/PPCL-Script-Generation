00010     C ---------------------------- PROGRAM AHU SMART FDD -----------------------------------
00020     C
00030     C *********************AIR HANDLER [07] SMART FDD***********************************
00040     C UNIT NOTES
00050     C UNIT HAS OA DAMPER, RA DAMPER, HW RE-HEAT, COOLING COILS, VAV BOXES
00060     C FANS WITH VFD, WITH SUPPLY AIR TEMPERATURE
00070     C AND DUCT STATIC PRESSURE CONTROL WITH OCCUPIED AND UNOCCUPIED MODES.
00080     C PANEL NAME - [0077-MA-AH07]
00090     C PANEL TYPE - [BACNET]
00100     C IP ADDRESS - 10.18.92.15
00110     C
00120     C
00130     C PANEL LOCATION: [ROOM RM225A]
00140     C DRAWING REFERENCES: [NUMBER]
00150     C
00160     C REV	DATE				AUTHOR			COMMENTS
00170     C A   	05/07/2020  	FDD TEAM    	INITIAL PROGRAMING
00180     C B
00190     C C
00200     C
00210     C	************ SMART FDD VIRTUAL POINTS*****************
00220     C "%A%_FDD"----------------------MAPPED VIRTUAL  LAO FOR FDD STATE
00230     C "%A%_FDD_OS"-----------------MAPPED VIRTUAL LAO FOR AHU OPERATING STATE
00240     C "%A%_FDD_TRN"----------------MAPPED VIRTUAL LAO FOR FDD TREND
00250     C "%A%_FDD_ALERT"--------------MAPPED VIRTUAL LDO FOR FDD ALERT
00260     C
00270     C **************EQUIPMENT POINTS******************
00280     C OCCUPIED PONT: ---------------"0077_AH07_OCC"
00290     DEFINE(OCC,"0077_AH07_OCC")
00300     C AHU FAN START/STOP: ---------"0077_AH07_SAF"
00310     DEFINE(FSS,"0077_AH07_SAF")
00320     C SUP_STATIC_PRS: ----------------"0077_AH07_SSP"
00330     DEFINE("STATIC_P","0077_AH07_SSP")
00340     C VFD_SPEED: ----------------------,"0077_AH07_SVD"
00350     DEFINE(VFDS,"0077_AH07_SVD")
00360     C CHW_VALVE: ---------------------"0077_AH07_CV"
00370     DEFINE(CHW,"0077_AH07_CV")
00380     C HW_VALVE: ----------------------"0077_AH07_HV"
00390     DEFINE(HW,"0077_AH07_HV")
00400     C RET_AIR_TEMP: ------------------"0077_AH07_RAT"
00410     DEFINE("RA_T","0077_AH07_RAT")
00420     C SUP_AIR_TEMP: ------------------"0077_AH07_SAT"
00430     DEFINE("SA_T","0077_AH07_SAT")
00440     C MIX_AIR_TEMP: -----------------"0077_AH07_MAT"
00450     DEFINE("MA_T","0077_AH07_MAT")
00460     C OA_AIR_TEMP:-------------------"0077_AH07_OA"
00470     DEFINE(OAZ,"0077_AH07_OA")
00480     C RET_AIR_TEMP_SP: ---------------"0077_AH07_RATS"
00490     DEFINE(RAS,"0077_AH07_RATS")
00500     C SUP_AIR_TEMP_SP: --------------"0077_AH07_SAT_SP"
00501     DEFINE(SAS,"0077_AH07_SAT_SP")
00520     C SUP_STATIC_PRS_SP: ------------"0077_AH07_SSPS"
00530     DEFINE(SPS,"0077_AH07_SSPS")
00540     C ROOM_TEMP: -------------------"0077_AH07_AVG_RMT"
00550     DEFINE(RMT,"0077_AH07_AVG_RMT")
00560     C ROOM_HUM: --------------------"RMH_POINT_NAME"
00570     C DEFINE(RMH,"RMH_POINT_NAME")
00580     C
00590     C **************FDD POINT VALUES*********************
00600     C FDD0 = NO FAULTS
00610     C FDD1 = OCCUPIED OPERATION ABNORMAL
00620     C FDD2 = STATIC PRESSURE CONTROL OUT OF RANGE
00630     C FDD3 = FAN COMMAND ABNORMAL
00640     C FDD4 = FAN SPEED ABORMAL
00650     C FDD5 = MAX OPERATING STATES REACHED IN AN HOUR
00660     C FDD6 = MIXED AIR TEMPERATURE OUT OF RANGE
00670     C FDD7 = SUPPLY AIR TEMPERATURE OUT OF RANGE
00680     C FDD8 = SUPPLY AIR TEMPERATURE TOO LOW WHEN HEATING
00690     C FDD9 = TEMPERATURE DROP ACROSS INACTIVE COOLING COIL
00700     C FDD10 = SUPPLY AIR TEMPERATURE TOO HIGH WHEN COOLING
00710     C FDD11 = TEMPERATURE RISE ACROSS INACTIVE HEATING COIL
00720     C FDD12 = ROOM TEMPERATURE EVALUATION
00730     C
00740     C EVERY ( 60 ) MINUTES EVALUATE FAULT ALERT
00750     C	ROTATE EXISTING FAULTS EVERY ( 5 ) SECONDS
00760     C
00770     ONPWRT(3270)
00780     C
00790     C *************** DEFINITIONS ************************
00800     DEFINE(A,"BUILDING_NAME")
00805     IF("%A%_FDD_PPCL_STATUS" .EQ. OFF) THEN GOTO 3270
00810     C *************** CREATE LOCAL VARIABLES **********************
00820     LOCAL(SSP,COS,"OCCUPIED",RAT,MAT,OAT,SAT,"RT_AVG")
00830     LOCAL(MAXFDD,FDD1,FDD2,FDD3,FDD4,FDD5,FDD6,FDD7,FDD8,FDD9,FDD10,FDD11,FDD12)
00850     C "%MA_T%" = "%RA_T%" * 0.8 + "%OAZ%" * 0.2
00860     C
00870     C FDD EVALUATION BEGINS
00880     C
00885     C ********************************BACNET RESET POINTS*****************************
00890     IF("%A%_FDD_1_RESET_POINT" .EQ. ON) THEN GOTO 891 ELSE GOTO 900
00891     INITTO(0,"%A%_FDD_1")
00892     RELEAS(@OPER,"%A%_FDD_1_RESET_POINT")
00895     INITTO(0,"%A%_FDD_1_RESET_POINT")
00900     IF("%A%_FDD_2_RESET_POINT" .EQ. ON) THEN GOTO 901 ELSE GOTO 910
00901     INITTO(0,"%A%_FDD_2")
00902     RELEAS(@OPER,"%A%_FDD_2_RESET_POINT")
00905     INITTO(0,"%A%_FDD_2_RESET_POINT")
00910     IF("%A%_FDD_3_RESET_POINT" .EQ. ON) THEN GOTO 911 ELSE GOTO 920
00911     INITTO(0,"%A%_FDD_3")
00912     RELEAS(@OPER,"%A%_FDD_3_RESET_POINT")
00915     INITTO(0,"%A%_FDD_3_RESET_POINT")
00920     IF("%A%_FDD_4_RESET_POINT" .EQ. ON) THEN GOTO 921 ELSE GOTO 930
00921     INITTO(0,"%A%_FDD_4")
00922     RELEAS(@OPER,"%A%_FDD_4_RESET_POINT")
00925     INITTO(0,"%A%_FDD_4_RESET_POINT")
00930     IF("%A%_FDD_5_RESET_POINT" .EQ. ON) THEN GOTO 931 ELSE GOTO 940
00931     INITTO(0,"%A%_FDD_5")
00932     RELEAS(@OPER,"%A%_FDD_5_RESET_POINT")
00935     INITTO(0,"%A%_FDD_5_RESET_POINT")
00940     IF("%A%_FDD_6_RESET_POINT" .EQ. ON) THEN GOTO 941 ELSE GOTO 950
00941     INITTO(0,"%A%_FDD_6")
00942     RELEAS(@OPER,"%A%_FDD_6_RESET_POINT")
00945     INITTO(0,"%A%_FDD_6_RESET_POINT")
00950     IF("%A%_FDD_7_RESET_POINT" .EQ. ON) THEN GOTO 951 ELSE GOTO 960
00951     INITTO(0,"%A%_FDD_7")
00952     RELEAS(@OPER,"%A%_FDD_7_RESET_POINT")
00955     INITTO(0,"%A%_FDD_7_RESET_POINT")
00960     IF("%A%_FDD_8_RESET_POINT" .EQ. ON) THEN GOTO 961 ELSE GOTO 970
00961     INITTO(0,"%A%_FDD_8")
00962     RELEAS(@OPER,"%A%_FDD_8_RESET_POINT")
00965     INITTO(0,"%A%_FDD_8_RESET_POINT")
00970     IF("%A%_FDD_9_RESET_POINT" .EQ. ON) THEN GOTO 971 ELSE GOTO 980
00971     INITTO(0,"%A%_FDD_9")
00972     RELEAS(@OPER,"%A%_FDD_9_RESET_POINT")
00975     INITTO(0,"%A%_FDD_9_RESET_POINT")
00980     IF("%A%_FDD_10_RESET_POINT" .EQ. ON) THEN GOTO 981 ELSE GOTO 990
00981     INITTO(0,"%A%_FDD_10")
00982     RELEAS(@OPER,"%A%_FDD_10_RESET_POINT")
00985     INITTO(0,"%A%_FDD_10_RESET_POINT")
00990     IF("%A%_FDD_11_RESET_POINT" .EQ. ON) THEN GOTO 991 ELSE GOTO 1000
00991     INITTO(0,"%A%_FDD_11")
00992     RELEAS(@OPER,"%A%_FDD_11_RESET_POINT")
00995     INITTO(0,"%A%_FDD_11_RESET_POINT")
01000     IF("%A%_FDD_12_RESET_POINT" .EQ. ON) THEN GOTO 1001 ELSE GOTO 1010
01001     INITTO(0,"%A%_FDD_12")
01002     RELEAS(@OPER,"%A%_FDD_12_RESET_POINT")
01005     INITTO(0,"%A%_FDD_12_RESET_POINT")
01010     C *****************************MONDAY RESET***************************************
01015     IF(DAY .EQ. 1 .AND. CRTIME .GE. 8.0 .AND. CRTIME .LE. 9.0) THEN GOTO 1020 ELSE GOTO 1130
01020     INITTO(0,"%A%_FDD_1")
01025     INITTO(0,"%A%_FDD_2")
01030     INITTO(0,"%A%_FDD_3")
01035     INITTO(0,"%A%_FDD_4")
01040     INITTO(0,"%A%_FDD_5")
01045     INITTO(0,"%A%_FDD_6")
01050     INITTO(0,"%A%_FDD_7")
01055     INITTO(0,"%A%_FDD_8")
01060     INITTO(0,"%A%_FDD_9")
01065     INITTO(0,"%A%_FDD_10")
01070     INITTO(0,"%A%_FDD_11")
01075     INITTO(0,"%A%_FDD_12")
01130     C
01140     C CHECK FOR OCCUPIED
01150     C
01155     IF("%A%_FDD_ALERT" .NE. @NONE) THEN RELEAS(@OPER,"%A%_FDD_ALERT")
01160     IF("%OCC%" .EQ. ON) THEN ON("$OCCUPIED")
01170     IF("%OCC%" .EQ. OFF) THEN OFF("$OCCUPIED")
01180     IF("$OCCUPIED" .NE. ON) THEN GOTO 3270
01190     C
01200     C ALERT EVALUATION
01210     C
01215     IF(DAY .EQ. 1 .AND. CRTIME .GE. 8.0 .AND. CRTIME .LE. 9.0) THEN INITTO(0,"0077_AHU7_FDD_PPCL_STATUS")
01220     ON("0077_AHU7_FDD_PPCL_STATUS")
01230     IF(SECND7 .GT. 1800) THEN GOTO 1250 ELSE GOTO 1280
01240     C
01250     SET(0.0,SECND7)
01260     IF("$MAXFDD" .GT. 0.0) THEN ON("%A%_FDD_ALERT") ELSE OFF("%A%_FDD_ALERT")
01270     C
01280     IF(SECND6 .GT. 60) THEN SET(0.0,SECND6)
01290     C
01300     C **************OPERATING STATES**************************
01310     C "0077_AH07_OA" = "BAC_7059_AI_0"
01320     C
01330     IF($COS .NE. "%A%_FDD_OS") THEN GOTO 1350
01340     GOTO 1380
01350     SAMPLE(900) GOTO 1370
01360     GOTO 3230
01370     $COS = "%A%_FDD_OS"
01380     LOCAL("DELTAOS","DELTAOSMAX")
01390     "$DELTAOSMAX" = 4
01400     SAMPLE(15) GOTO 1450
01410     GOTO 1690
01420     C
01430     C HEATING MODE
01440     C
01450     IF("%HW%" .GT. 0.0 .AND. "%CHW%" .EQ. 0.0 .AND. "%A%_FDD_OS" .NE. 1) THEN GOTO 1470
01460     GOTO 1520
01470     SET(1.0,"%A%_FDD_OS")
01480     "$DELTAOS" = "$DELTAOS" + 1
01490     C
01500     C COOLING MODE
01510     C
01520     IF("%HW%" .LT. 1.0 .AND. "%CHW%" .GT. 0.0 .AND. "%A%_FDD_OS" .NE. 2) THEN GOTO 1540
01530     GOTO 1590
01540     SET(2.0,"%A%_FDD_OS")
01550     "$DELTAOS" = "$DELTAOS" + 1
01560     C
01570     C DEHUMIDIFICATION MODE
01580     C
01590     IF("%HW%" .GT. 0 .AND. "%CHW%" .GT. 0) THEN GOTO 1600 ELSE GOTO 1650
01600     IF("%A%_FDD_OS" .NE. 3) THEN GOTO 1620
01610     GOTO 1690
01620     SET(3.0,"%A%_FDD_OS")
01630     "$DELTAOS" = "$DELTAOS" + 1
01640     C
01650     IF("%A%_FDD_OS" .EQ. 1 .AND. "0077_AH07_HW_AVAIL" .NE. ON) THEN GOTO 3270
01660     IF("%A%_FDD_OS" .EQ. 2 .AND. "0077_AH07_CHW_AVAIL" .NE. ON) THEN GOTO 3270
01670     IF("%A%_FDD_OS" .EQ. 3 .AND. "0077_AH07_CHW_AVAIL" .NE. ON .AND. "0077_AH07_HW_AVAIL" .NE. ON) THEN GOTO 3270
01680     C ****************GATHER DATA SECTION*************************
01690     C
01700     TIMAVG($SSP,300,5,"%STATIC_P%")
01710     TIMAVG($RAT,300,5,"%RA_T%")
01720     TIMAVG($OAT,300,5,"%OAZ%")
01730     TIMAVG($MAT,300,5,"%MA_T%")
01740     TIMAVG($SAT,300,5,"%SA_T%")
01750     TIMAVG("$RT_AVG",300,5,"%RMT%")
01760     C
01770     C FDD1: OCCUPIED VS FAN START/STOP EVAUATION
01780     C
01790     IF(SECND6 .GT. 5 .AND. SECND6 .LT. 10) THEN GOTO 1800 ELSE GOTO 1840
01800     SET(0.0,$FDD1)
01810     IF("%OCC%" .EQ. ON .AND. "%FSS%" .EQ. OFF) THEN SET(1.0,$FDD1)
01820     IF($FDD1 .NE. 0.0) THEN "%A%_FDD" = $FDD1
01830     C
01840     C FDD2: STATIC PRESSURE EVALUATION
01850     C
01860     IF(SECND6 .GT. 10 .AND. SECND6 .LT. 15) THEN GOTO 1870 ELSE GOTO 1910
01870     SET(0.0,$FDD2)
01880     IF($SSP .LE. "%SPS%" - 0.2) THEN SET(2.0,$FDD2)
01890     IF($SSP .GE. "%SPS%" + 0.2) THEN SET(2.0,$FDD2)
01900     IF($FDD2 .NE. 0.0) THEN "%A%_FDD" = $FDD2
01910     C
01920     C FDD3: FAN COMMAND EVALUATION
01930     C
01940     IF(SECND6 .GT. 15 .AND. SECND6 .LT. 20) THEN GOTO 1950 ELSE GOTO 1990
01950     SET(0.0,$FDD3)
01960     IF("%FSS%" .NE. @NONE) THEN SET(3.0,$FDD3)
01970     IF($FDD3 .NE. 0.0) THEN "%A%_FDD" = $FDD3
01980     C
01990     C FDD4: FAN SPEED EVAULATION
02000     C
02010     IF(SECND6 .GT. 20.0 .AND. SECND6 .LT. 25) THEN GOTO 2020 ELSE GOTO 2070
02020     SET(0.0,$FDD4)
02030     IF("%VFDS%" .GT. 80.0 .OR. "%VFDS%" .EQ. @OPER) THEN SET(4.0,$FDD4)
02040     IF("%VFDS%" .LT. 30 .OR. "%VFDS% .EQ. @OPER) THEN SET(4.0,$FDD4)
02050     IF($FDD4 .NE. 0.0) THEN "%A%_FDD" = $FDD4
02060     C
02070     C CHECK FOR OPERATING CHANGES EVERY HOUR
02080     C
02090     IF(SECND5 .GT. 3600) THEN GOTO 2110 ELSE GOTO 2200
02100     C
02110     SET(0.0,SECND5)
02120     C
02130     C FDD5: OPERATING STATES CHANGES EVALUATION IN ONE HOUR
02140     C
02150     IF("$DELTAOS" .GT. "$DELTAOSMAX") THEN SET(5.0,$FDD5) ELSE SET(0.0,$FDD5)
02160     IF($FDD5 .NE. 0.0) THEN "%A%_FDD" = $FDD5
02170     SET(0.0,"$DELTAOS")
02180     C
02190     C FDD6: MIXED AIR TEMPERATURE EVALUATION
02200     C
02210     LOCAL(MINMAT,MAXMAT)
02220     IF("%OAZ%" .EQ. FAILED) THEN GOTO 2310
02230     MIN("$MINMAT",$RAT,$OAT)
02240     MAX("$MAXMAT",$RAT,$OAT)
02250     IF(SECND6 .GT. 25 .AND. SECND6 .LT. 30) THEN GOTO 2260 ELSE GOTO 2310
02260     SET(0.0,$FDD6)
02270     IF($MAT + 5.0 .LT. "$MINMAT" - 2.0) THEN SET(6.0,$FDD6)
02280     IF($MAT - 5.0 .GT. "$MAXMAT" + 2.0) THEN SET(6.0,$FDD6)
02290     IF($FDD6 .NE. 0.0) THEN "%A%_FDD" = $FDD6
02300     C
02310     C FDD7: SUPPLY AIR TEMPERATURE SETPOINT EVALUATION
02320     C
02330     IF(SECND6 .GT. 30 .AND. SECND6 .LT. 35) THEN GOTO 2340 ELSE GOTO 2390
02340     SET(0.0,$FDD7)
02350     IF($SAT .GT. "%SAS%" + 2.0) THEN SET(7.0,$FDD7)
02360     IF($SAT .LT. "%SAS%" - 2.0) THEN SET(7.0,$FDD7)
02370     IF($FDD7 .NE. 0.0) THEN "%A%_FDD" = $FDD7
02380     C
02390     C ***** HEATING EVALUATION **********
02400     C
02410     IF(SECND6 .GT. 35.0 .AND. SECND6 .LT. 40) THEN GOTO 2420 ELSE GOTO 2590
02420     IF("%A%_FDD_OS" .EQ. 1) THEN GOTO 2470 ELSE GOTO 2590
02430     C
02440     C FDD 8: SUPPLY AIR TEMPERATURE EVALUATION WHEN HEATING
02450     C
02460     C DELTATSF = 2F, EPSSAT = 2F, EPSMAT = 5F
02470     SET(0.0,$FDD8)
02480     IF($SAT + 2.0 .LE. $MAT - 5.0 + 2.0) THEN SET(8.0,$FDD8)
02490     IF($FDD8 .NE. 0.0) THEN "%A%_FDD" = $FDD8
02500     C
02510     C FDD9: TEMPERATURE DROP EVALUATION ACROSS INACTIVE COOLING COIL
02520     C
02530     C SQRT(EPSCCET^2+EPSCCLT^2) = SQRT(EPSMAT^2+EPSSAT^2) = SQRT(5^2+2^2) = 5.4
02540     C 5.4 + DELTASF = 7.4
02550     SET(0.0,$FDD9)
02560     IF($MAT - $SAT .GE. 7.4) THEN SET(9.0,$FDD9)
02570     IF($FDD9 .NE. 0.0) THEN "%A%_FDD" = $FDD9
02580     SET(0.0,$FDD10,$FDD11)
02590     C ****** COOLING EVALUATION ********
02600     C
02610     IF(SECND6 .GT. 45.0 .AND. SECND6 .LT. 50) THEN GOTO 2620 ELSE GOTO 2800
02620     IF("%A%_FDD_OS" .EQ. 2) THEN GOTO 2660 ELSE GOTO 2800
02630     C
02640     C FDD10: SUPPLY AIR TEMPERATURE EVALUATION WHEN COOLING
02650     C
02660     SET(0.0,$FDD10)
02670     IF($SAT - 2.0 - 2.0 .GT. $MAT + 5.0) THEN SET(10.0,$FDD10)
02680     IF($FDD10 .NE. 0.0) THEN "%A%_FDD" = $FDD10
02690     C
02700     C FDD11: TEMPERATURE RISE VALUATION ACCROSS INCATIVE HEATING COIL
02710     C
02720     C SQRT(EPSHCET^2+EPSHCLT^2) = SQRT(EPSSAT^2+EPSMAT^2) = SQRT(5^2_2^2) = 5.4
02730     C 5.4 + DELTASF = 7.4
02740     SET(0.0,$FDD11)
02750     IF($SAT - $MAT .GE. 7.4) THEN SET(11.0,$FDD11)
02760     IF($FDD11 .NE. 0.0) THEN "%A%_FDD" = $FDD11
02770     SET(0.0,$FDD8,$FDD9)
02780     C
02790     C ******** HUMIDITY EVALUATION ***************
02800     IF("%A%_FDD_OS" .EQ. 3) THEN GOTO 2810 ELSE GOTO 2820
02810     SET(0.0,$FDD8,$FDD9,$FDD10,$FDD11)
02820     C
02830     C  FDD12: ROOM TEMPERATURE EVAULATION
02840     C
02850     SET(0.0,$FDD12)
02860     IF("$RT_AVG" .GT. 75.0) THEN SET(12.0,$FDD12)
02870     IF("$RT_AVG" .LT. 70.0) THEN SET(12.0,$FDD12)
02880     IF($FDD12 .NE. 0.0) THEN "%A%_FDD" = $FDD12
02890     C
02900     C TREND POINT
02910     C
02912     IF("BLDG_UNIT_KW_UNLOAD" .EQ. OFF) THEN GOTO 2920
02914     SET(0.0, $FDD2, $FDD4)
02920     IF(SECND6 .GE. 60) THEN GOTO 2930 ELSE GOTO 3220
02930     IF($FDD1 .GT. 0) THEN ON("%A%_FDD_1") ELSE OFF("%A%_FDD_1")
02950     IF($FDD2 .GT. 0) THEN ON("%A%_FDD_2") ELSE OFF("%A%_FDD_2")
02970     IF($FDD3 .GT. 0) THEN ON("%A%_FDD_3") ELSE OFF("%A%_FDD_3")
02990     IF($FDD4 .GT. 0) THEN ON("%A%_FDD_4") ELSE OFF("%A%_FDD_4")
03010     IF($FDD5 .GT. 0) THEN ON("%A%_FDD_5") ELSE OFF("%A%_FDD_5")
03030     IF($FDD6 .GT. 0) THEN ON("%A%_FDD_6") ELSE OFF("%A%_FDD_6")
03050     IF($FDD7 .GT. 0) THEN ON("%A%_FDD_7") ELSE OFF("%A%_FDD_7")
03070     IF($FDD8 .GT. 0) THEN ON("%A%_FDD_8") ELSE OFF("%A%_FDD_8")
03090     IF($FDD9 .GT. 0) THEN ON("%A%_FDD_9") ELSE OFF("%A%_FDD_9")
03110     IF($FDD10 .GT. 0) THEN ON("%A%_FDD_10") ELSE OFF("%A%_FDD_10")
03130     IF($FDD11 .GT. 0) THEN ON("%A%_FDD_11") ELSE OFF("%A%_FDD_11")
03150     IF($FDD12 .GT. 0) THEN ON("%A%_FDD_12") ELSE OFF("%A%_FDD_12")
03170     C
03180     C GET MAXIMUM FAULT
03190     C
03200     MAX("%A%_FDD",$FDD1,$FDD2,$FDD3,$FDD4,$FDD5,$FDD6,$FDD7,$FDD8,$FDD9,$FDD10,$FDD11,$FDD12)
03210     MAX("$MAXFDD",$FDD1,$FDD2,$FDD3,$FDD4,$FDD5,$FDD6,$FDD7,$FDD8,$FDD9,$FDD10,$FDD11,$FDD12)
03220     C
03230     GOTO 3310
03240     C
03250     C CLEAR
03260     C
03270     SET(0.0,SECND6,SECND7,SECND5)
03272     OFF(@NONE, "%A%_FDD_1", "%A%_FDD_2", "%A%_FDD_3", "%A%_FDD_4", "%A%_FDD_5", "%A%_FDD_6", "%A%_FDD_7")
03275     OFF(@NONE, "%A%_FDD_8", "%A%_FDD_9", "%A%_FDD_10", "%A%_FDD_11", "%A%_FDD_12", "%A%_FDD_13")
03290     SET(0.0,"%A%_FDD")
03300     OFF("%A%_FDD_ALERT")
03310     GOTO 260