00100     C ---------------------------- PROGRAM AHU 100%OA SMART FDD -----------------------------------
00110     C
00120     C *********************AIR HANDLER [] SMART FDD***********************************
00130     C UNIT NOTES
00140     C
00150     C
00160     C
00170     C PANEL NAME - []
00180     C PANEL TYPE - []
00190     C IP ADDRESS - xx.xx.xx.xx
00200     C
00210     C PANEL LOCATION: [ROOM NUMBER]
00220     C DRAWING REFERENCES: [NUMBER]
00230     C
00240     C	REV	DATE				AUTHOR			COMMENTS
00250     C	A   	10/28/2020  	FDD TEAM    	INITIAL PROGRAMING
00260     C	B
00270     C	C
00280     C
00290     C	************SMART FDD VIRTUAL POINTS*****************
00300     C "%A%_FDD"----------------------MAPPED VIRTUAL  LAO FOR FDD STATE
00310     C "%A%_FDD_OS"-----------------MAPPED VIRTUAL LAO FOR AHU OPERATING STATE
00320     C "%A%_FDD_TRN"----------------MAPPED VIRTUAL LAO FOR FDD TREND
00330     C "%A%_FDD_ALERT"--------------MAPPED VIRTUAL LDO FOR FDD ALERT
00340     C COMMENTED DEFINE FUNCTIONS ARE POINTS NOT USED IN THIS UNIT
00350     C **************EQUIPMENT POINTS******************
00360     C OCCUPIED PONT: ---------------""
00370     DEFINE(OCC,"")
00380     C AHU FAN START/STOP: ---------""
00390     DEFINE(FSS,"")
00400     C SUP_STATIC_PRS: ----------------""
00410     DEFINE("STATIC_P","")
00420     C VFD_SPEED: ----------------------""
00430     DEFINE(VFDS,"")
00440     C CHW_VALVE: ---------------------""
00450     DEFINE(CHW,"")
00460     C PHW_VALVE: ----------------------""
00470     DEFINE(PHW,"")
00500     C SUP_AIR_TEMP: ------------------""
00510     DEFINE("SA_T","")
00520     C PREHEAT_TEMP: -----------------""
00530     DEFINE("PH_T","")
00540     C OA_AIR_TEMP:-------------------""
00550     DEFINE(OAZ,"")
00560     C PREHEAT SET POINT: ---------------""
00570     DEFINE("PHS","")
00580     C SUP_AIR_TEMP_SP: --------------""
00590     DEFINE(SAS,"")
00600     C SUP_STATIC_PRS_SP: ------------""
00610     DEFINE(SPS,"")
00652     C OA_FLOW: ---------------------"OAFL_POINT_NAME"
00654     DEFINE(OAFL, "OAFL_POINT_NAME")
00656     C OA_FLOW_SP: ---------------------"OAFLSP_POINT_NAME"
00658     DEFINE(OAFLSP, "OAFLSP_POINT_NAME")
00660     C **************FDD POINT VALUES*********************
00670     C FDD0 = NO FAULTS
00680     C FDD1 = OCCUPIED OPERATION ABNORMAL
00690     C FDD2 = STATIC PRESSURE CONTROL OUT OF RANGE
00700     C FDD3 = FAN COMMAND ABNORMAL
00710     C FDD4 = FAN SPEED ABORMAL
00720     C FDD5 = MAX OPERATING STATES REACHED IN AN HOUR
00730     C FDD6 = PREHEAT AIR TEMPERATURE EVALUATION
00740     C FDD7 = SUPPLY AIR TEMPERATURE OUT OF RANGE
00750     C FDD8 = SUPPLY AIR TEMPERATURE EVALUATION WHEN HEATING
00760     C FDD9 = TEMPERATURE DROP ACROSS INACTIVE COOLING COIL
00770     C FDD10 = SUPPLY AIR TEMPERATURE TOO HIGH WHEN COOLING
00780     C FDD11 = TEMPERATURE RISE ACROSS INACTIVE HEATING COIL
00790     C FDD14 = OUTSIDE AIRFLOW OUT OF RANGE
00810     C
00820     C 	EVERY ( 60 ) MINUTES EVALUATE FAULT ALERT
00830     C	ROTATE EXISTING FAULTS EVERY ( 5 ) SECONDS
00840     C
00850     ONPWRT(3230)
00860     C
00870     C *************** DEFINITIONS ************************
00880     DEFINE(A,"BLDG_UNIT")
00885     IF("%A%_FDD_PPCL_STATUS" .EQ. OFF) THEN GOTO 3230
00890     C *************** CREATE LOCAL VARIABLES **********************
00900     LOCAL(SSP,COS,RAT,PHT,OAT,SAT,"RT_AVG","DELTAOS","DELTAOSMAX",MINMAT,MAXMAT,"RH_AVG","OCCUPIED",OAF)
00910     LOCAL(FDD1,FDD2,FDD3,FDD4,FDD5,FDD6,FDD7,FDD8,FDD9,FDD10,FDD11,FDD14)
00950     C
00960     C FDD EVALUATION BEGINS
00970     C *****************************THURSDAY RESET***************************************
00980     IF(DAY .EQ. 4 .AND. CRTIME .GE. 8.0 .AND. CRTIME .LE. 9.0) THEN GOTO 990 ELSE GOTO 1130
00990     INITTO(0,"%A%_FDD_1")
01000     INITTO(0,"%A%_FDD_2")
01010     INITTO(0,"%A%_FDD_3")
01020     INITTO(0,"%A%_FDD_4")
01030     INITTO(0,"%A%_FDD_5")
01040     INITTO(0,"%A%_FDD_6")
01050     INITTO(0,"%A%_FDD_7")
01060     INITTO(0,"%A%_FDD_8")
01070     INITTO(0,"%A%_FDD_9")
01080     INITTO(0,"%A%_FDD_10")
01090     INITTO(0,"%A%_FDD_11")
01107     INITTO(0,"%A%_FDD_14")
01110     C
01120     C CHECK FOR OCCUPIED
01130     C
01140     WAIT(1800,"%OCC%","$OCCUPIED",11)
01150     IF("%OCC%" .EQ. OFF) THEN OFF("$OCCUPIED")
01160     IF("$OCCUPIED" .NE. ON) THEN GOTO 3230
01170     C
01180     C ALERT EVALUATION
01190     IF("%A%_FDD_ALERT" .NE. @NONE) THEN RELEAS(@OPER,"%A%_FDD_ALERT")
01200     C
01220     IF(SECND7 .GT. 1800) THEN GOTO 1240 ELSE GOTO 1280
01230     C
01240     SET(0.0,SECND7)
01250     IF("%A%_FDD" .GT. 0.0) THEN ON("%A%_FDD_ALERT") ELSE OFF("%A%_FDD_ALERT")
01260     IF("%A%_FDD_ALERT" .EQ. OFF) THEN INITTO(0,"%A%_FDD_ALERT")
01270     C
01280     IF(SECND6 .GT. 60) THEN SET(0.0,SECND6)
01290     C
01300     C **************OPERATING STATES**************************
01310     C
01320     IF($COS .NE. "%A%_FDD_OS") THEN GOTO 1340
01330     GOTO 1370
01340     SAMPLE(600) GOTO 1360
01350     GOTO 3190
01360     $COS = "%A%_FDD_OS"
01380     "$DELTAOSMAX" = 4
01390     SAMPLE(15) GOTO 1440
01400     GOTO 1680
01410     C
01420     C HEATING MODE
01430     C
01440     IF("%PHW%" .GT. 1.0 .AND. "%CHW%" .LT. 1.0 .AND. "%A%_FDD_OS" .NE. 1) THEN GOTO 1460
01450     GOTO 1510
01460     SET(1.0,"%A%_FDD_OS")
01470     "$DELTAOS" = "$DELTAOS" + 1
01480     C
01490     C COOLING MODE
01500     C
01510     IF("%PHW%" .LT. 1.0 .AND. "%CHW%" .GT. 1.0 .AND. "%A%_FDD_OS" .NE. 2) THEN GOTO 1530
01520     GOTO 1580
01530     SET(2.0,"%A%_FDD_OS")
01540     "$DELTAOS" = "$DELTAOS" + 1
01550     C
01560     C DEHUMIDIFICATION MODE
01570     C
01580     IF("%PHW%" .GT. 1.0 .AND. "%CHW%" .GT. 1.0) THEN GOTO 1590 ELSE GOTO 1640
01590     IF("%A%_FDD_OS" .NE. 3) THEN GOTO 1610
01600     GOTO 1640
01610     SET(3.0,"%A%_FDD_OS")
01620     "$DELTAOS" = "$DELTAOS" + 1
01630     C
01640     IF("%A%_FDD_OS" .EQ. 1 .AND. "CHEM_HW_ENABLE" .NE. ON) THEN GOTO 3230
01650     IF("%A%_FDD_OS" .EQ. 2 .AND. "CHEM_CWENA" .NE. ON) THEN GOTO 3230
01660     IF("%A%_FDD_OS" .EQ. 3 .AND. "CHEM_CWENA" .NE. ON .AND. "CHEM_HW_ENABLE" .NE. ON) THEN GOTO 3230
01670     C ****************GATHER DATA SECTION*************************
01680     C
01690     TIMAVG($SSP,300,5,"%STATIC_P%")
01710     TIMAVG($OAT,300,5,"%OAZ%")
01720     TIMAVG($PHT,300,5,"%PH_T%")
01730     TIMAVG($SAT,300,5,"%SA_T%")
01735     TIMAVG("$OAF", 300, 5, "%OAFL%")
01740     C
01750     C FDD1: OCCUPIED VS FAN START/STOP EVAUATION
01760     C
01770     IF(SECND6 .GT. 5 .AND. SECND6 .LT. 10) THEN GOTO 1780 ELSE GOTO 1820
01780     SET(0.0,$FDD1)
01790     IF("%OCC%" .EQ. ON .AND. "%FSS%" .EQ. OFF) THEN SET(1.0,$FDD1)
01800     IF($FDD1 .NE. 0.0) THEN "%A%_FDD" = $FDD1
01810     C
01820     C FDD2: STATIC PRESSURE EVALUATION
01830     C
01840     IF(SECND6 .GT. 10 .AND. SECND6 .LT. 15) THEN GOTO 1850 ELSE GOTO 1890
01850     SET(0.0,$FDD2)
01860     IF($SSP .LE. "%SPS%" - 0.2) THEN SET(2.0,$FDD2)
01870     IF($SSP .GE. "%SPS%" + 0.2) THEN SET(2.0,$FDD2)
01880     IF($FDD2 .NE. 0.0) THEN "%A%_FDD" = $FDD2
01890     C
01900     C FDD3: FAN COMMAND EVALUATION
01910     C
01920     IF(SECND6 .GT. 15 .AND. SECND6 .LT. 20) THEN GOTO 1930 ELSE GOTO 1970
01930     SET(0.0,$FDD3)
01940     IF("%FSS%" .NE. @NONE .AND. "%FSS%" .EQ. OFF) THEN SET(3.0,$FDD3)
01950     IF($FDD3 .NE. 0.0) THEN "%A%_FDD" = $FDD3
01960     C
01970     C FDD4: FAN SPEED EVAULATION
01980     C
01990     IF(SECND6 .GT. 20.0 .AND. SECND6 .LT. 25) THEN GOTO 2000 ELSE GOTO 2050
02000     SET(0.0,$FDD4)
02010     IF("%VFDS%" .GT. 80.0 .OR. "%VFDS%" .EQ. @OPER) THEN SET(4.0,$FDD4)
02020     IF("%VFDS%" .LT. 35 .OR. "%VFDS%" .EQ. @OPER) THEN SET(4.0,$FDD4)
02030     IF($FDD4 .NE. 0.0) THEN "%A%_FDD" = $FDD4
02040     C
02050     C CHECK FOR OPERATING CHANGES EVERY HOUR
02060     C
02070     IF(SECND5 .GT. 3600) THEN GOTO 2090 ELSE GOTO 2280
02080     C
02090     SET(0.0,SECND5)
02100     C
02110     C FDD5: OPERATING STATES CHANGES EVALUATION IN ONE HOUR
02120     C
02130     IF("$DELTAOS" .GT. "$DELTAOSMAX") THEN SET(5.0,$FDD5) ELSE SET(0.0,$FDD5)
02140     IF($FDD5 .NE. 0.0) THEN "%A%_FDD" = $FDD5
02150     SET(0.0,"$DELTAOS")
02280     C
02290     C FDD7: SUPPLY AIR TEMPERATURE SETPOINT EVALUATION
02300     C
02310     IF(SECND6 .GT. 30 .AND. SECND6 .LT. 35) THEN GOTO 2320 ELSE GOTO 2370
02320     SET(0.0,$FDD7)
02330     IF($SAT .GT. "%SAS%" + 2.0) THEN SET(7.0,$FDD7)
02340     IF($SAT .LT. "%SAS%" - 2.0) THEN SET(7.0,$FDD7)
02350     IF($FDD7 .NE. 0.0) THEN "%A%_FDD" = $FDD7
02360     C
02370     C ***** HEATING EVALUATION **********
02380     C
02390     IF(SECND6 .GT. 35.0 .AND. SECND6 .LT. 40) THEN GOTO 2400 ELSE GOTO 2570
02400     IF("%A%_FDD_OS" .EQ. 1) THEN GOTO 2401 ELSE GOTO 2570
02401     C
02402     C FDD6: PREHEAT AIR TEMPERATURE EVALUATION
02403     C
02404     IF("%OAZ%" .EQ. FAILED) THEN GOTO 2440
02407     SET(0.0,$FDD6)
02408     IF($PHT .LT. "%PHS%" - 2.0) THEN SET(6.0,$FDD6)
02409     IF($PHT + 2.0 .LT. $OAT - 5.0) THEN SET(6.0,$FDD6)
02410     IF($FDD6 .NE. 0.0) THEN "%A%_FDD" = $FDD6
02411     C
02412     C FDD 8: SUPPLY AIR TEMPERATURE EVALUATION WHEN HEATING
02430     C
02440     C DELTATSF = 2F, EPSSAT = 2F, EPSMAT = 5F
02450     SET(0.0,$FDD8)
02460     IF($SAT + 2.0 .LE. $PHT - 5.0 + 2.0) THEN SET(8.0,$FDD8)
02470     IF($FDD8 .NE. 0.0) THEN "%A%_FDD" = $FDD8
02480     C
02490     C FDD9: TEMPERATURE DROP EVALUATION ACROSS INACTIVE COOLING COIL
02500     C
02510     C SQRT(EPSCCET^2+EPSCCLT^2) = SQRT(EPSMAT^2+EPSSAT^2) = SQRT(5^2+2^2) = 5.4
02520     C 5.4 + DELTASF = 7.4
02530     SET(0.0,$FDD9)
02540     IF($PHT - $SAT .GE. 7.4) THEN SET(9.0,$FDD9)
02550     IF($FDD9 .NE. 0.0) THEN "%A%_FDD" = $FDD9
02560     SET(0.0,$FDD10,$FDD11)
02570     C ****** COOLING EVALUATION ********
02580     C
02590     IF(SECND6 .GT. 45.0 .AND. SECND6 .LT. 50) THEN GOTO 2600 ELSE GOTO 2780
02600     IF("%A%_FDD_OS" .EQ. 2) THEN GOTO 2640 ELSE GOTO 2780
02610     C
02620     C FDD10: SUPPLY AIR TEMPERATURE EVALUATION WHEN COOLING
02630     C
02640     SET(0.0,$FDD10)
02650     IF($SAT - 2.0 - 2.0 .GT. $PHT + 5.0) THEN SET(10.0,$FDD10)
02660     IF($FDD10 .NE. 0.0) THEN "%A%_FDD" = $FDD10
02670     C
02680     C FDD11: TEMPERATURE RISE VALUATION ACCROSS INCATIVE HEATING COIL
02690     C
02700     C SQRT(EPSHCET^2+EPSHCLT^2) = SQRT(EPSSAT^2+EPSMAT^2) = SQRT(5^2_2^2) = 5.4
02710     C 5.4 + DELTASF = 7.4
02720     SET(0.0,$FDD11)
02730     IF($SAT - $PHT .GE. 7.4) THEN SET(11.0,$FDD11)
02740     IF($FDD11 .NE. 0.0) THEN "%A%_FDD" = $FDD11
02750     SET(0.0,$FDD8,$FDD9,$FDD6)
02760     C
02770     C ******** DEHUMIDITY EVALUATION ***************
02780     IF("%A%_FDD_OS" .EQ. 3) THEN GOTO 2790 ELSE GOTO 2960
02790     SET(0.0,$FDD6,$FDD8,$FDD9,$FDD10,$FDD11)
02951     C
02952     C FDD14: OUTSIDE AIRFLOW EVALUATION
02953     C
02954     IF(SECND6 .GT. 57 .AND. SECND6 .LT. 60) THEN GOTO 2955 ELSE GOTO 2960
02955     SET(0.0,$FDD14)
02956     IF($OAF .LE. "%OAFLSP%"*0.8) THEN SET(14.0,$FDD14)
02957     IF($OAF .GE. "%OAFLSP%"*1.2) THEN SET(14.0,$FDD14)
02958     IF($FDD14 .NE. 0.0) THEN "%A%_FDD" = $FDD14
02960     C
02970     C TREND POINT
02980     C
02982     IF("BLDG_UNIT_KW_UNLOAD" .EQ. OFF) THEN GOTO 2990
02984     SET(0.0, $FDD2, $FDD4) 
02990     IF(SECND6 .GE. 60) THEN GOTO 3000 ELSE GOTO 3180
03000     IF($FDD1 .GT. 0) THEN ON("%A%_FDD_1") ELSE OFF("%A%_FDD_1")
03010     IF($FDD2 .GT. 0) THEN ON("%A%_FDD_2") ELSE OFF("%A%_FDD_2")
03020     IF($FDD3 .GT. 0) THEN ON("%A%_FDD_3") ELSE OFF("%A%_FDD_3")
03030     IF($FDD4 .GT. 0) THEN ON("%A%_FDD_4") ELSE OFF("%A%_FDD_4")
03040     IF($FDD5 .GT. 0) THEN ON("%A%_FDD_5") ELSE OFF("%A%_FDD_5")
03050     IF($FDD6 .GT. 0) THEN ON("%A%_FDD_6") ELSE OFF("%A%_FDD_6")
03060     IF($FDD7 .GT. 0) THEN ON("%A%_FDD_7") ELSE OFF("%A%_FDD_7")
03070     IF($FDD8 .GT. 0) THEN ON("%A%_FDD_8") ELSE OFF("%A%_FDD_8")
03080     IF($FDD9 .GT. 0) THEN ON("%A%_FDD_9") ELSE OFF("%A%_FDD_9")
03090     IF($FDD10 .GT. 0) THEN ON("%A%_FDD_10") ELSE OFF("%A%_FDD_10")
03100     IF($FDD11 .GT. 0) THEN ON("%A%_FDD_11") ELSE OFF("%A%_FDD_11")
03125     IF($FDD14 .GT. 0) THEN ON("%A%_FDD_14") ELSE OFF("%A%_FDD_14")
03130     C
03140     C GET MAXIMUM FAULT
03150     C
03160     MAX("%A%_FDD",$FDD1,$FDD2,$FDD3,$FDD4,$FDD5,$FDD6,$FDD7,$FDD8,$FDD9,$FDD10,$FDD11,$FDD14)
03180     C
03190     GOTO 3270
03200     C
03210     C CLEAR
03220     C
03230     SET(0.0,SECND6,SECND7,SECND5)
03250     SET(0.0,"%A%_FDD")
03252     OFF(@NONE, "%A%_FDD_1", "%A%_FDD_2", "%A%_FDD_3", "%A%_FDD_4", "%A%_FDD_5", "%A%_FDD_6", "%A%_FDD_7")
03255     OFF(@NONE, "%A%_FDD_8", "%A%_FDD_9", "%A%_FDD_10", "%A%_FDD_11", "%A%_FDD_12", "%A%_FDD_13", "%A%_FDD_14")
03260     OFF("%A%_FDD_ALERT")
03270     GOTO 330